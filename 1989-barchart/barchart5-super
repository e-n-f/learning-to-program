0  print  chr$ (4)"BLOAD SMALL.FONT,A$4000": poke 232,0: poke 233,64
1  text : home 
2  scale= 1
5  print "RANDOM/CHOSEN (R/C): ";: get R$:RAN = R$ = "C"
6  rem 




         **************************
7  rem  *     SUPER.BARCHART     *
8  rem  *    Graphing program    *
10  rem *                        *
11  rem *    by  Eric Fischer    *
12  rem *                        *
13  rem **************************



14  hgr 
15  home : vtab 24
20  input "NUMBER OF BARS: ";NB
23 W = (279 / (NB + 3))
25  dim H(NB),L$(NB)
27  input "MAX HEIGHT: ";MH:HH = (160 - W) / MH
28  input "BACKDROP (NOTHING FOR BLANK): ";M$: if M$ = "" then 30
29  print  chr$ (4)"BLOAD "M$",A$2000"
30 :
32  for N = 1 to NB
33  on RAN + 1 goto 35,40
35 H =  int (MH *  rnd (1)) + 1:H(N) = H * HH: for Z = 1 to  rnd (1) * 5 + 1:L$(N) = L$(N) +  chr$ ( rnd (1) * 60 + 64): next : goto 50
40  print "BAR #"N;: input ": ";H:H(N) = HH * H: input "LABEL: ";L$(N): print 
50  next 
55  if M$ = "" then  hcolor= 1: hplot 0,0: call 62454: gosub 1000
57  home : gosub 5000
58  gosub 4000
59  home : poke 230,32: poke  - 16304,0: poke  - 16301,0: poke  - 16297,0: poke  - 16300,0
60  for N = NB to 1 step  - 1
65 H = (N * W + W) / 7 + 1:V = H(N) / HH: gosub 3000
70  for H = 159 to 160 - H(N) step  - 1
80  hcolor= 5
90  hplot N * W + W,H to N * W,H - W
100  hcolor= 7: hplot N * W + W,H to N * W + W * 2 - 2,H
105  hcolor= 4: hplot N * W + W * 2 - 1,H
110  next 
115  hcolor= 6
120  for A = 0 to W - 1
130  hplot N * W + W - A,H - A to N * W + W * 2 - A - 1,H - A
140  next 
141  hcolor= 7: hplot N * W + W - 0,H - 0 to N * W + W - (W - 1),H - (W - 1)
142  hplot N * W + W * 2 - 1,H to N * W + W * 2 - (W - 1) - 1,H - (W - 1)
145  hcolor= 4
147  if  len (L$(N)) = 0 then 160
150  rot= 48: for ZX = 1 to 3: for ZY = 1 to 3: hcolor= (ZX = 2 and ZY = 2) * 3 + 4: xdraw 100 at N * W + W + ZX,155 - ZY: for L = 1 to  len (L$(N)): draw  asc ( mid$ (L$(N),L,1)) - 31: xdraw 99: next : next : next 
160  hcolor= 7: xdraw 100 at N * W + W + 2,155 - 2: for L = 1 to  len (L$(N)): draw  asc ( mid$ (L$(N),L,1)) - 31: xdraw 99: next 
165  poke  - 16302,0
170  rot= 0: xdraw 100 at N * W + W + 2,160:ZZ$ =  str$ (H(N) / HH): for L = 1 to  len (ZZ$): draw  asc ( mid$ (ZZ$,L,1)) - 31: draw 99: next 
180  if N <  > 1 then  hplot N * W + W,160 to N * W + W,168
200  next 
205  print : vtab 24
206  get K$: poke  - 16301,0
207  poke  - 16368,0
210  print "Name of graph to save on disk"
220  input "(blank not to save):         ";N$
230  if N$ <  > "" then  print  chr$ (4)"BSAVE "N$",A$2000,L$2000"
999  end 
1000  for A = 0 to 160 - W step 2: hcolor= 6: hplot 0,A to 279,A: next 
1001  for A = 1 to 160 - W step 2: hcolor= 7: hplot 0,A to 279,A: next 
1002  for A = 160 - W to 160 step 2: hcolor= 5: hplot 0,A to 279,A: next 
1003  for A = 161 - W to 160 step 2: hcolor= 4: hplot 0,A to 279,A: next 
1004  for A = W * 2 to (NB + 2) * W + 1 step W: hcolor= 7: hplot A,160 to A - W,160 - W: hplot A -  int (W),160 -  int (W) to A -  int (W),0: hplot A -  int (W) + 1,160 -  int (W) to A -  int (W) + 1,0
1005  next 
1010  hplot 0,159 to 279,159: hplot 0,160 - W to 279,160 - W
1015  hcolor= 4: for Y = 160 to 191: hplot 0,Y to 279,Y: next 
1020  poke  - 16301,0: print "Display horizontal scale lines? ": get K$: if K$ = "n" or K$ = "N" then  return 
1030  input "Spaced how far apart? ";SS
1040  for SZ = 160 - W to 0 step  - (SS * HH)
1050  hcolor= 4: hplot 0,SZ to 279,SZ
1060  next 
1999  return 
3000  return 
4000  text : home 
4010  print "1. Change values"
4020  print "2. Draw graph"
4030  get K$: if K$ = "1" or K$ = "2" then 4050
4040  goto 4000
4050  if K$ = "2" then  return 
4055  print "Values: "
4056  for N = 1 to NB: print "#"N": "H(N) / HH,L$(N): if  peek ( - 16384) > 127 then  poke  - 16368,0:N = NB + 1
4057  next 
4060  input "Change which bar:";N$:N =  val (N$)
4070  if N > NB or N < 1 or N <  >  int (N) then 4000
4080  print "Current value: ";H(N) / HH
4085  print "Current label: ";L$(N)
4090  input "New value:     ";H:H(N) = H * HH
4095  input "New label:     ";L$:L$(N) = L$
4100  goto 4000
5000  text : home : input "LINES IN TITLE: ";TL
5003 RM = 279
5005  if TL > 10 then  print "MAXIMUM 10": goto 5000
5007  if TL < 1 then  return 
5010  dim T$(TL)
5020  for M = 1 to TL
5030  print "LINE #"M": ";: input "";T$(M): next 
5040 :
5050  for M = 1 to TL
5060  if T$(M) = "" then 5100
5070  rot= 32: xdraw 100 at 279,M * 8 + 8
5080  for N = 1 to  len (T$(M)): xdraw  asc ( mid$ (T$(M),N,1)) - 31: xdraw 99: next 
5090  call 62923: if  peek (224) + 256 *  peek (225) < RM then RM =  peek (224) + 256 *  peek (225)
5100  next 
5105 RM = RM - 2
5110  rot= 0: hcolor= 4: for M = 0 to TL * 8 + 16 + 2: hplot RM,M to 279,M: next 
5120  hcolor= 7: hplot RM,0 to 279,0 to 279,TL * 8 + 18 to RM,TL * 8 + 18 to RM,0
5125  hplot RM - 1,1 to RM - 1,TL * 8 + 19 to 278,TL * 8 + 19
5130  for M = 1 to TL
5135  if T$(M) = "" then 5160
5140  xdraw 100 at RM + 2,M * 8
5150  for N = 1 to  len (T$(M)): xdraw  asc ( mid$ (T$(M),N,1)) - 31: xdraw 99: next 
5160  next : return 
